grammar org.eclipse.xtext.example.EcoreDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate ecoredsl "http://example.xtext.org/EcoreDsl"

EcoreDsl: (metamodelDeclarations+=ReferencedMetamodel)* package=EPackageDecl;

ReferencedMetamodel : "import" (alias=ID "=")? ePackage=[ecore::EPackage|STRING] ";" ;

EPackageDecl returns ecore::EPackage: (eAnnotations+=EAnnotationDecl)* "package" name=QID "nsURI" "=" nsURI=STRING "nsPrefix" "=" nsPrefix=QID  "{" 
    (eSubpackages+=SubEPackageDecl | eClassifiers+=EClassifierDecl)*  "}"
;

SubEPackageDecl returns ecore::EPackage : (eAnnotations+=EAnnotationDecl)* "package" name=ID "{" 
    (eSubpackages+=SubEPackageDecl | eClassifiers+=EClassifierDecl)*  "}"
;

EClassifierDecl returns ecore::EClassifier: 
    EClassDecl | EDataTypeDecl
;

EDataTypeDecl returns ecore::EDataType : 
    (eAnnotations+=EAnnotationDecl)*  
    (serializable=Serializable)? ("datatype" name=ID ":" instanceClassName=STRING_OR_QID ";") | EEnumDecl
;

EAnnotationDecl returns ecore::EAnnotation: 
    "@" source=STRING_OR_QID ("(" details+=MapEntry ("," details+=MapEntry)* ")")? ;

EClassDecl returns ecore::EClass : (eAnnotations+=EAnnotationDecl)*  (^abstract?="abstract")?  ((interface?="interface")|"class") name=ID 
    ("<" eTypeParameters+=ETypeParameterDecl ("," eTypeParameters+=ETypeParameterDecl)* ">")?
    ("extends" eGenericSuperTypes+=EGenericTypeReferenceDecl ("," eGenericSuperTypes+=EGenericTypeReferenceDecl)*)?  (":" instanceClassName=STRING_OR_QID)? "{"  
    ((eStructuralFeatures+=EStructuralFeatureDecl)|(eOperations+=EOperationDecl))* "}"
;

EStructuralFeatureDecl returns ecore::EStructuralFeature:
   EAttributeDecl | EReferenceDecl
;
            
EAttributeDecl returns ecore::EAttribute:
    (eAnnotations+=EAnnotationDecl)* 
    ((iD?="ID")|(unique=Bag)|(ordered=Random)|(changeable=Readonly)|(volatile?="volatile")|(transient?="transient")|(unsettable?="unsettable")|(derived?="derived"))*
    "attr" eGenericType=EGenericTypeReferenceDecl ("["lowerBound=INT (".." upperBound=SINT)? "]")?  name=ID ("=" defaultValueLiteral=STRING)?  ";" 
;

EReferenceDecl returns ecore::EReference:
    (eAnnotations+=EAnnotationDecl)* 
    ((resolveProxies=Local)|(unique=Bag)|(ordered=Random)|(changeable=Readonly)|(volatile?="volatile")|(transient?="transient")|(unsettable?="unsettable")|(derived?="derived"))*
    ((containment?="val")|"ref") eGenericType=EGenericTypeReferenceDecl ("["lowerBound=INT (".." upperBound=SINT)? "]")? ("#" eOpposite=[ecore::EReference])? name=ID ";" 
;

EEnumDecl returns ecore::EEnum: (eAnnotations+=EAnnotationDecl)*  "enum" name=ID "{" 
    (eLiterals+=EEnumLiteralDecl)+ "}"
;

EEnumLiteralDecl returns ecore::EEnumLiteral : 
    (eAnnotations+=EAnnotationDecl)*  name=ID ("=" value=INT)? (literal=STRING)? ";"
;

ETypeParameterDecl returns ecore::ETypeParameter :
    name=ID ("extends" eBounds+=EGenericTypeDecl)?
;

EGenericTypeReferenceDecl returns ecore::EGenericType:
    (eClassifier=[ecore::EClassifier] ("<" eTypeArguments+=EGenericTypeDecl ("," eTypeArguments+=EGenericTypeDecl)* ">")? ) 
    | ("#"eTypeParameter=[ecore::ETypeParameter])
;

EGenericTypeDecl returns ecore::EGenericType:
    (eClassifier=[ecore::EClassifier] ("<" eTypeArguments+=EGenericTypeDecl ("," eTypeArguments+=EGenericTypeDecl)* ">")? ) 
    | ("#"eTypeParameter=[ecore::ETypeParameter])
    | ("?"(("extends" eUpperBound=EGenericTypeDecl) | ("super" eLowerBound=EGenericTypeDecl)) )
;

EOperationDecl returns ecore::EOperation:
   (eAnnotations+=EAnnotationDecl)*  
   ((unique?=Bag)|(ordered?=Random))*
   "op"  ((eGenericType=EGenericTypeReferenceDecl)|"void") name=ID
   ("<" eTypeParameters+=ETypeParameterDecl ("," eTypeParameters+=ETypeParameterDecl)* ">")? 
   "(" (eParameters+=EParameterDecl (","  eParameters+=EParameterDecl)* )? ")" ("throws" eGenericExceptions+=EGenericTypeReferenceDecl ("," eGenericExceptions+=EGenericTypeReferenceDecl)*  )? ";"  
;

EParameterDecl returns ecore::EParameter:
    (eAnnotations+=EAnnotationDecl)*  
    eGenericType=EGenericTypeReferenceDecl name=ID
;

MapEntry returns ecore::EStringToStringMapEntry:
   key=ID "=" value=STRING 
;

QID: ID (("."|"$") ID)*;

STRING_OR_QID : STRING | QID ;

SINT returns ecore::EInt :
       '-'? INT;

terminal Bag returns ecore::EBoolean :
	"bag"
;

terminal Random returns ecore::EBoolean :
	"random"
;

terminal Readonly returns ecore::EBoolean :
	"readonly"
;

terminal Local returns ecore::EBoolean :
	"local"
;

terminal Serializable returns ecore::EBoolean :
	"!serializable"
;
