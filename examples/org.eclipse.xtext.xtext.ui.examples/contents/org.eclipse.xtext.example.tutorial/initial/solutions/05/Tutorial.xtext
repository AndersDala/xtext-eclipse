/*******************************************************************************
 * Copyright (c) 2012 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.example.tutorial.Tutorial with org.eclipse.xtext.xbase.Xbase

generate tutorial "http://www.eclipse.org/xtext/example/tutorial/Tutorial"

DomainModelTutorial:
	(elements += AbstractElement)*
;

PackageDeclaration:
	'package' name = QualifiedName '{'
		( elements += AbstractElement )*
	'}'
;

AbstractElement:
	PackageDeclaration | Entity | Import
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

QualifiedNameWithWildcard:
	QualifiedName '.*'?
;

Entity:
	'entity' name = ValidID
	('extends' superType=JvmTypeReference)? 
	'{'
		// We call a new rule Feature instead of Property.
		(features += Feature)*
	'}'
;

// A Feature is a Property or an Operation.
Feature:
	Property | Operation
;

Property:
	name=ValidID ':' type=JvmTypeReference
; 

// The new parser rule for Operations. 
Operation:
	// An Operation has a name...
	'op' name=ValidID 
		// ... a couple of parameters (note the pattern for comma separated lists) ...
		'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
		// ... and a return type. 
		':' type=JvmTypeReference
		// An Operation's body is an XBlockExpression from Xbase.
		// With this simple call, you have all the power of a full blown expression language in your DSL. 
		body=XBlockExpression
;
