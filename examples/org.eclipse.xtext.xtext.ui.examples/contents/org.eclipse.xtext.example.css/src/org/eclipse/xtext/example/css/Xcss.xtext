grammar org.eclipse.xtext.example.css.Xcss with org.eclipse.xtext.xbase.Xbase

generate xcss "http://www.eclipse.org/xtext/example/css/Xcss"
import "http://www.eclipse.org/xtext/xbase/Xbase"
import "http://www.eclipse.org/xtext/common/JavaVMTypes"

StyleSheet: {StyleSheet}
	rules+=StyleRule*;

StyleRule:
	selectors+=Selector+ '{'
		settings+=Setting*
	'}';

Selector:
	(TypeSelector | WildcardSelector) filter=Filter?;

_JvmTypeReference returns JvmTypeReference:
  TypeSelector
;

TypeSelector:
	type=[JvmType|QualifiedName];

WildcardSelector:
	{WildcardSelector} '*';

Filter:
	'[' condition=XExpression ']';

Setting returns XExpression:
	XOrExpression ({XAssignment.assignable=current} feature=[JvmIdentifyableElement|OpSettingAssign] value=XAssignment)? ';';
//	assignable=FeatureReference feature=[JvmIdentifyableElement|OpAssign] value=XExpression ';';

OpSettingAssign:
	':';

FeatureReference returns XFeatureCall:
	feature=[JvmIdentifyableElement];

CssLiterals returns XExpression:
	FontLiteral | ColorLiteral | SizeLiteral;

FontLiteral:
	{FontLiteral}
	'font' name=StringOrID?
	(
	  (italic?='I')?
	& (bold?='B')?
	& (size?=SizeLiteral)?
	);

StringOrID:
	STRING | ID;

ColorLiteral:
	ColorConstant | RGB;

ColorConstant:
	'#' constant=[JvmIdentifyableElement];

RGB:
	'#' color=INT;

SizeLiteral:
	value=INT unit=SizeUnit;

enum SizeUnit:
	em | pt | px | percent='%';

XPrimaryExpression returns XExpression:
	CssLiterals |
	XClosure |
	XBooleanLiteral |
	XIntLiteral |
	XNullLiteral |
	XStringLiteral |
	XTypeLiteral |
	XConstructorCall |
	XBlockExpression |
	XSwitchExpression |
	XFeatureCall |
	XIfExpression |
	XForLoopExpression |
	XWhileExpression |
	XDoWhileExpression |
	XCastedExpression |
	XThrowExpression |
	XTryCatchFinallyExpression |
	XParenthesizedExpression;
